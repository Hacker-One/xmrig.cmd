@ECHO OFF
SETLOCAL ENABLEEXTENSIONS
CLS
TITLE XMRig

SET URL=pool.monero.hashvault.pro
SET PORT=5555
SET WALLET=4...
SET DIFF=10000
SET ID=%COMPUTERNAME%
SET EMAIL=user@server.com

SET ALLOW_MANUAL_SELECT=true

SET SRC=%~dp0
SET PROGRAM_TITLE=XMRig
SET PROGRAM_PATH=%SRC:~0,-1%
SET PROGRAM_CPU_FILENAME=xmrig.exe
SET PROGRAM_GPU_FILENAME=xmrig-nvidia.exe
SET PROGRAM_PARAMETERS=--algo=cryptonight --url=%URL%:%PORT% --keepalive --retries=5 --retry-pause=5 --donate-level=1 --nicehash 
SET PROGRAM_CPU_PARAMETERS=--av=1 --threads=4 --cpu-affinity=0xAA
SET PROGRAM_GPU_PARAMETERS=--cuda-devices=0 --cuda-launch=16x4 --cuda-bfactor=6 --cuda-bsleep=50

SET TASKKILL=%SystemRoot%\System32\taskkill.exe
SET TASKLIST=%SystemRoot%\System32\tasklist.exe
SET CSCRIPT=%SystemRoot%\System32\cscript.exe

IF "%1" EQU "ELEVATE" (
	CALL :SELECT %2 %3
) ELSE (
	CALL :SELECT %1 %2
)
IF "%PARAMETER%" EQU "CPU" (
	SET PROGRAM_PATH=%PROGRAM_PATH%\CPU
	SET PROGRAM_FILENAME=%PROGRAM_CPU_FILENAME%
	SET PROGRAM_PARAMETERS=%PROGRAM_PARAMETERS% %PROGRAM_CPU_PARAMETERS%
)
IF "%PARAMETER%" EQU "GPU" (
	SET PROGRAM_PATH=%PROGRAM_PATH%\GPU
	SET PROGRAM_FILENAME=%PROGRAM_GPU_FILENAME%
	SET PROGRAM_PARAMETERS=%PROGRAM_PARAMETERS% %PROGRAM_GPU_PARAMETERS%
)
CLS
IF "%PROGRAM_FILENAME%" EQU "" GOTO END
IF EXIST "%PROGRAM_PATH%\%PROGRAM_FILENAME%" (
	IF EXIST "%CSCRIPT%" (
		IF "%1" EQU "ELEVATE" (
			CALL :TEST "ELEVATE" "%PARAMETER%" "%ACTION%"
		) ELSE (
			CALL :TEST "NONE" "%PARAMETER%" "%ACTION%"
		)
	) ELSE (
		ECHO.
		ECHO ERROR: Can not find "%CSCRIPT%".
		CALL :START
	)
)
GOTO END

:SELECT
	IF "%~1" NEQ "" (
		IF "%~1" EQU "CPU" (
			SET PARAMETER=CPU
		) ELSE IF "%~1" EQU "GPU" (
			SET PARAMETER=GPU
		) ELSE IF "%~1" EQU "START" (
			SET ACTION=START
		) ELSE IF "%~1" EQU "STOP" (
			SET ACTION=STOP
		)
	)
	IF "%~2" NEQ "" (
		IF "%~2" EQU "CPU" (
			SET PARAMETER=CPU
		) ELSE IF "%~2" EQU "GPU" (
			SET PARAMETER=GPU
		) ELSE IF "%~2" EQU "START" (
			SET ACTION=START
		) ELSE IF "%~2" EQU "STOP" (
			SET ACTION=STOP
		)
	)
	IF "%ALLOW_MANUAL_SELECT%" NEQ "true" GOTO END
	IF "%PARAMETER%" EQU "" (
		SET /P PARAMETER="Please select a program [CPU/GPU]: "
	)
	IF "%PARAMETER%" NEQ "CPU" (
		IF "%PARAMETER%" NEQ "GPU" (
			SET PARAMETER=
			ECHO Select is not correct. Please input "CPU" or "GPU". Try again...
			ECHO.
			GOTO :SELECT
		)
	)
	IF "%ACTION%" EQU "" (
		SET /P ACTION="Please select an action [START/STOP]: "
	)
	IF "%ACTION%" NEQ "START" (
		IF "%ACTION%" NEQ "STOP" (
			SET ACTION=
			ECHO Select is not correct. Please input "START" or "STOP". Try again...
			ECHO.
			GOTO :SELECT "%PARAMETER%"
		)
	)
GOTO END

:TEST
	NET SESSION >NUL 2>&1
	IF "%ERRORLEVEL%" EQU "0" (
		CALL :START "ELEVATE"
	) ELSE (
		IF "%~1" EQU "ELEVATE" (
			IF NOT EXIST "%TASKLIST%" (
				CALL :START
			) ELSE (
				IF NOT EXIST "%TASKKILL%" CALL :START
			)
		) ELSE (
			IF EXIST "%TASKLIST%" (
				IF EXIST "%TASKKILL%" CALL :START
			)
			CALL :ELEVATE %~2 %~3
		)
	)
GOTO END

:ELEVATE
	ECHO CreateObject^("Shell.Application"^).ShellExecute "%~snx0","ELEVATE %~1 %~2","%~sdp0","runas","%PROGRAM_TITLE%">"%TEMP%\%~n0.vbs"
	%CSCRIPT% //nologo "%TEMP%\%~n0.vbs"
	IF EXIST "%TEMP%\%~n0.vbs" DEL "%TEMP%\%~n0.vbs"
GOTO END


:START
	CALL :CHECK
	IF "%ACTION%" EQU "STOP" GOTO END
	SET USER=%WALLET%
	IF "%DIFF%" NEQ "" SET USER=%USER%+%DIFF%
	SET PASSWORD=%ID%-%PARAMETER%
	IF "%EMAIL%" NEQ "" SET PASSWORD=%PASSWORD%:%EMAIL%
	SET PROGRAM_PARAMETERS=--user=%USER% --pass=%PASSWORD% %PROGRAM_PARAMETERS%
	CD "%PROGRAM_PATH%"
	IF "%~1" EQU "ELEVATE" (
		CALL "%PROGRAM_PATH%\%PROGRAM_FILENAME%" %PROGRAM_PARAMETERS%
	) ELSE (
		START "%PROGRAM_TITLE%" /D "%PROGRAM_PATH%" "%PROGRAM_FILENAME%" %PROGRAM_PARAMETERS%
	)
GOTO END

:CHECK
	IF EXIST "%TASKLIST%" (
		FOR /F "tokens=2 delims=," %%A IN ('%TASKLIST% /FI "ImageName EQ %PROGRAM_FILENAME%" /FO:CSV /NH^| FIND /I "%PROGRAM_FILENAME%"') DO SET TASK_PID=%%~A
	) ELSE (
		ECHO.
		ECHO ERROR: Can not find "%TASKLIST%".
	)
	IF "%TASK_PID%" NEQ "" (
		IF EXIST "%TASKKILL%" (
			%TASKKILL% /F /IM "%PROGRAM_FILENAME%">NUL
		) ELSE (
			ECHO.
			ECHO ERROR: Can not find "%TASKKILL%".
		)
	)
GOTO END

:END
GOTO :EOF
